( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( Retro                                                       )
( Copyright [c] 2009, Charles Childers                        )
( License: ISC                                                )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
3072 constant SCRATCH-START
3584 constant TIB
4096 constant HEAP-START
5000000 constant MAX-IMAGE
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
mark-dictionary    ( Pointer to the most recent dictionary    )
variable last      ( header                                   )
HEAP-START
variable: heap     ( Starting address of the data/code heap   )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
variable which     ( Pointer to dictionary header of the most )
                   ( recently looked up word                  )
variable compiler  ( Is the compiler on or off?               )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
variable flag      ( Shared variable used by some primitives  )
                   ( It's used as a flag, a counter, etc.     )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
label: copytag   " RETRO 10" $,
label: nomatch   " Word Not Found" $,
label: okmsg     " ok " $,
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
t: dup  ( n-nn )  dup, ;       t: 1+   ( n-n   )  1+, ;
t: 1-   ( n-n  )  1-, ;        t: swap ( xy-yx )  swap, ;
t: drop ( n-   )  drop, ;      t: and  ( xy-n  )  and, ;
t: or   ( xy-n )  or, ;        t: xor  ( xy-n  )  xor, ;
t: @    ( a-n  )  @, ;         t: !    ( na-   )  !, ;
t: +    ( xy-n )  +, ;         t: -    ( xy-n  )  -, ;
t: *    ( xy-n )  *, ;         t: /mod ( xy-qr )  /mod, ;
t: <<   ( xy-n )  <<, ;        t: >>   ( xy-n  )  >>, ;
t: out  ( np-  )  out, ;       t: in   ( p-n   )  in, ;

t: wait ( - )  0 # 0 # out, wait, ;

t: nip   ( xy-y   )  swap, drop, ;
t: over  ( xy-xyx )  push, dup, pop, swap, ;
t: 2drop ( nn-    )  drop, drop, ;
t: not   ( x-y    )  -1 # xor, ;
t: rot   ( xyz-yzx ) push, swap, pop, swap, ;
t: -rot  ( xyz-xzy ) swap, push, swap, pop, ;
t: tuck  ( xy-yxy )  dup, -rot ;
t: 2dup  ( x-xx   )  over over ;
t: on    ( a-     )  -1 # swap, !, ;
t: off   ( a-     )  0 # swap, !, ;
t: /     ( xy-q   )  /mod, nip ;
t: mod   ( xy-r   )  /mod, drop, ;
t: neg   ( x-y    )  -1 # *, ;
t: execute ( a-   )  1-, push, ;
t: later ( - )  pop, pop, swap, push, push, ;
t: @+ ( a-an )  dup, 1+, swap, @, ;
t: !+ ( na-a )  dup, 1+, push, !, pop, ;
t: +! ( na-  )  dup, push, @, +, pop, !, ;
t: -! ( na-  )  dup, push, @, swap, -, pop, !, ;
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
t: t-here   ( -a  )  heap # @, ;
t: t-,      ( n-  )  t-here !, t-here 1+, heap # !, ;
t: t-]      ( -   )  -1 # compiler # !, ;
t: t-[      ( -   )  0 # compiler # !, ;
t: t-;;     ( -   )  9 # t-, ;
t: t-;      ( -   )  t-;; t-[ ;
t: ($,)     ( a-a )  repeat dup, @, 0; t-, 1+, again ;
t: $        ( a-  )  ($,) drop, 0 # t-, ;
t: t-push   ( n-  )  5 # t-, ;
t: t-pop    ( -n  )  6 # t-, ;
t: compile  ( a-  )  7 # t-, t-, ;
t: literal, ( n-  )  1 # t-, t-, ;
t: t-for    ( R: n-   C: -a )
  t-here 5 # t-, ;
t: t-next   ( R: -    C: a- )
  6 # t-, 27 # t-, 2 # t-, 1 # t-, 0 # t-, 12 # t-, t-, 3 # t-, ;
t: (if)     ( -a )
  t-here 0 # t-, ;
t: t-=if    ( R: xy-  C: -a )
  12 # t-, (if) ;
t: t->if    ( R: xy-  C: -a )
  11 # t-, (if) ;
t: t-<if    ( R: xy-  C: -a )
  10 # t-, (if) ;
t: t-!if    ( R: xy-  C: -a )
  13 # t-, (if) ;
t: t-then   ( R: -    C: a- )
  t-here swap, !, ;
t: t-repeat ( R: -    C: -a )
  t-here ;
t: t-again  ( R: -    C: a- )
  8 # t-, t-, ;
t: t-0;     ( n-n || n -  )  25 # t-, ;

t: .word   ( a- )
   compiler # @, -1 # =if 7 # t-, t-, ; then execute ;
t: .macro  ( a- )
   execute ;
t: .data   ( a- )
   compiler # @, -1 # =if 1 # t-, t-, then ;

' .word 'WORD set-class
' .macro 'MACRO set-class
' .data 'DATA set-class
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
-1 variable: update
t: redraw ( -  ) update # @, 0; drop, 0 # 3 # out, ;
t: emit   ( c- )  1 # 2 # out, wait redraw ;
t: cr     ( -  ) 10 # emit ;
t: clear  ( -  ) -1 # emit ;
t: (type) ( a-a ) repeat @+ 0; emit again ;
t: type   ( a-  ) update # off (type) drop, update # on redraw ;
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
variable >in             ( Offset into the TIB                )
variable break-char      ( Holds the delimiter for 'accept'   )
-1 variable: whitespace  ( Allow extended whitespace          )

t: (remap-keys) ( c-c ) ;
t: ws ( c-c )
  dup, 13 # =if drop, 10 # then
  whitespace # @, 0; drop,
  dup,  9 # =if drop, 32 # ; then
  dup, 10 # =if drop, 32 # ; then
;

t: key ( -c )
  repeat
    1 # 1 # out,
    wait 1 # in,
    dup, 0 # !if (remap-keys) ws ; then drop,
  again
;

t: >tib ( c- )  TIB # >in # @, +, !, 1 # >in # +! ;

t: eat-leading ( - )
  repeat
    key dup, emit dup,
    break-char # @, !if >tib ; then drop,
  again ;

t: (accept) ( -c )
  repeat
    key dup, emit dup,
    break-char # @, =if drop, ; then
    dup, 8 # =if 1 # >in # -! drop, 8 m, ' (accept) m, then
    >tib
  again ;

t: accept ( c- )
  break-char # !, 0 # >in # !, eat-leading (accept) 0 # >tib ;
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
t: d->class ( a-a )  1+, ;
t: d->xt    ( a-a )  1+, 1+, ;
t: d->name  ( a-a )  1+, 1+, 1+, ;
t: create   ( "-  )   t-here             ( Entry Start )
                     last # @, t-,       ( Link to previous )
                     last # !,           ( Set as newest )
                     ' .data # t-,       ( Class = .data )
                     t-here 0 # t-,      ( XT )
                     32 # accept TIB # $ ( Name )
                     t-here swap, !, ;   ( Patch XT to HERE )
t: (:)        ( -   )  last # @, d->class !, t-] 0 # t-, 0 # t-, ;
t: :          ( "-  )  create ' .word # (:) ;
t: t-(        ( "-  )  char: ) # accept ;
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
t: n=n      ( xy-   )  !if 0 # flag # !, then ;
t: get-set  ( ab-xy )  @, swap, @, ;
t: next-set ( ab-cd )  1+, swap, 1+, swap, ;

t: (skim)
  repeat
    2dup @, swap, @, +, 0; drop,
    2dup get-set n=n next-set
    flag # @, 0; drop,
  again
;

t: compare   ( $$-f )
  -1 # flag # !,
  (skim) 2drop flag # @, ;

t: (strlen)  ( a-na )
  repeat dup, @, 0; drop, next-set again ;

t: getLength ( $-n )
  0 # swap, (strlen) drop, ;

variable SAFE

t: (reset-$)  SCRATCH-START # SAFE # !, ;
t: (next)     1 # SAFE # +! ;
t: (save)     repeat @+ 0; SAFE # @, !, (next) again ;

t: tempString  ( a-a )
  (reset-$) (save) drop, 0 # SAFE # @, !, SCRATCH-START # ;

t: (save) dup, getLength repeat 0; push, @+ t-, pop, 1-, again ;

t: keepString  ( a-a )
  dup, getLength 1+, 1+, 1+, t-here +,
  8 # t-, t-,
  t-here push,
  (save) drop, 0 # t-,
  pop,
;

t: t-"  ( "-a )
  char: " # accept TIB # tempString ;
t: t-s" ( R: -a  C: "- )
  t-" keepString 1 # t-, t-, ;
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
variable #value        variable num
variable #ok           variable negate?

label: nums " 0123456789ABCDEF" $,
label: base 10 m,

t: digits
  repeat dup, push, nums # +, @, over =if -1 # num # !, then pop, 0; 1-, again ;
t: (digit)
  base # @, 10 # =if 10 # digits ; then
  base # @, 16 # =if 16 # digits ; then
  base # @,  8 # =if  8 # digits ; then
  base # @,  2 # =if  2 # digits ; then
;
t: digit?
  0 # num # !, (digit) drop, num # @, ;

t: char>digit ( c-n )
  char: 0 # -,
  base # @, 16 # =if dup, 16 # >if 7 # -, then then ;

t: isNegative?
  ( a-a+1 )
  dup, @, char: - # =if -1 # negate? # !, 1+, ; then
  1 # negate? # !, ;

t: (convert)
  repeat
    dup, @, 0; char>digit #value # @, base # @, *, +,
    #value # !, 1+,
  again ;

t: >number ( $-n )
  isNegative? 0 # #value # !, (convert) drop,
  #value # @, negate? # @, *, ;

t: (isnumber)
  repeat
    dup, @, 0; digit? flag # @, and, flag # !, 1+,
  again ;

t: isnumber?
  isNegative? -1 # flag # !, (isnumber) drop,
  flag # @, ;

t: <#> repeat base # @, /mod, swap, nums # +, @, swap, 0; again ;
t: neg? dup, 0 # >if ; then 45 # emit -1 # *, ;
t: display repeat 0; emit again ;
t: . neg? 0 # swap <#> display 32 # emit ;
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
variable found

t: (search)
  repeat
    dup, d->name TIB # compare
    -1 # =if which # !, found # on ; then
    @, 0;
  again
;

t: search
  found # off last # @, (search) ;

t: t-'    ( "-a )
  32 # accept search
  found # @, -1 # =if which # @, d->xt @, ; then 0 #
  found # on ;
t: t-[']  ( R: -a   C: "- )
  1 # t-, t-' t-, ;
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
t: :devector ( a-  )  dup, 0 # swap, !, 1+, 0 # swap, !, ;
t: :is       ( aa- )  dup, 8 # swap, !, 1+, !, ;
t: devector  ( "-  )  t-' :devector ;
t: is        ( a"- )  t-' :is ;
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
t: save   ( - )  1 # 4 # out, wait ;
t: bye    ( - )  halt, ;
t: words  ( - )
  last # @, repeat dup, d->name type 32 # emit @, 0; again ;
t: depth  ( -n )
  -5 # 5 # out, wait, 5 # in, ;
t: reset  ( ...- )
  depth repeat 0; push, drop, pop, 1-, again ;
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
variable #mem   ( Amount of memory provided )
variable fb     ( canvas present?    )
variable fw     ( framebuffer width  )
variable fh     ( framebuffer height )

t: boot ( - )
  copytag # type cr ;
t: run-on-boot ( - )
  -1 # 5 # out, wait 5 # in, #mem # !,  ( Memory Size )
  -2 # 5 # out, wait 5 # in, fb # !,    ( Canvas Present? )
  -3 # 5 # out, wait 5 # in, fw # !,    ( Canvas Width )
  -4 # 5 # out, wait 5 # in, fh # !,    ( Canvas Height )
  boot ;
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
t: with-class   ( ac- ) execute ;
t: notfound     ( -   ) cr nomatch # type cr ;
t: the->xt      ( a-n ) which # @, d->xt @, ;
t: the->class   ( a-n ) which # @, d->class @, ;
t: tib->number? ( -f  ) TIB # isnumber? ;
t: tib->number  ( -n  ) TIB # >number .data ;

t: word?   ( - )
  found # @, -1 # =if the->xt the->class with-class then ;
t: number? ( - )
  found # @, 0 # =if tib->number? -1 # =if tib->number ; then
  notfound then ;
t: ok      ( - )
  compiler # @, 0 # =if cr okmsg # type then ;
t: listen  ( - )
  repeat ok 32 # accept search word? number? again ;

main:
  run-on-boot listen ;

' 1+           word: 1+            ' 1-           word: 1-
' swap         word: swap          ' drop         word: drop
' and          word: and           ' or           word: or
' xor          word: xor           ' @            word: @
' !            word: !             ' +            word: +
' -            word: -             ' *            word: *
' /mod         word: /mod          ' <<           word: <<
' >>           word: >>            ' nip          word: nip
' dup          word: dup           ' in           word: in
' out          word: out           ' accept       word: accept
' t-here       word: here          ' t-,          word: ,
' t-]          word: ]             ' create       word: create
' :            word: :             ' later        word: later
' cr           word: cr            ' emit         word: emit
' type         word: type          ' clear        word: clear
' words        word: words         ' key          word: key
' over         word: over          ' 2drop        word: 2drop
' not          word: not           ' rot          word: rot
' -rot         word: -rot          ' tuck         word: tuck
' 2dup         word: 2dup          ' on           word: on
' off          word: off           ' /            word: /
' mod          word: mod           ' neg          word: neg
' execute      word: execute       ' .            word: .
' t-"          word: "             ' compare      word: compare
' wait         word: wait          ' t-'          word: '
' @+           word: @+            ' !+           word: !+
' +!           word: +!            ' -!           word: -!
' :is          word: :is           ' :devector    word: :devector
' is           word: is            ' devector     word: devector
' compile      word: compile       ' literal,     word: literal,
' tempString   word: tempString    ' redraw       word: redraw
' keepString   word: keepString    ' getLength    word: getLength
' bye          word: bye           ' (remap-keys) word: (remap-keys)
' with-class   word: with-class    ' .word        word: .word
' .macro       word: .macro        ' .data        word: .data
' d->class     word: d->class      ' d->xt        word: d->xt
' d->name      word: d->name       ' boot         word: boot
' depth        word: depth         ' reset        word: reset
' notfound     word: notfound      ' save         word: save
' >number      word: >number       ' ok           word: ok
' listen       word: listen        ' isnumber?    word: isNumber?

' t-s"         macro: s"        ' t-[          macro: [
' t-;          macro: ;         ' t-;;         macro: ;;
' t-=if        macro: =if       ' t->if        macro: >if
' t-<if        macro: <if       ' t-!if        macro: !if
' t-then       macro: then      ' t-repeat     macro: repeat
' t-again      macro: again     ' t-0;         macro: 0;
' t-push       macro: push      ' t-pop        macro: pop
' t-[']        macro: [']       ' t-for        macro: for
' t-next       macro: next      ' t-(          macro: (

  last         data: last          compiler     data: compiler
  TIB          data: tib           update       data: update
  fb           data: fb            fw           data: fw
  fh           data: fh            #mem         data: #mem
  heap         data: heap          which        data: which
  whitespace   data: whitespace    base         data: base

patch-dictionary
boot-new
